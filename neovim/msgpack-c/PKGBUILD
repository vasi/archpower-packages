# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Baptiste Jonglez
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=6.1.0
pkgrel=2
pkgdesc='An efficient object serialization library'
arch=("x86_64" 'powerpc')
url='https://msgpack.org/'
license=('BSL-1.0')
depends=('glibc')
makedepends=(
  'cmake'
  'doxygen'
  'graphviz'
)
checkdepends=('gtest')
provides=(libmsgpack-c.so)
source=("$pkgname-$pkgver.tar.gz::https://github.com/msgpack/msgpack-c/archive/c-$pkgver.tar.gz")
b2sums=('f244fe26b44b1985e4bd87e37c0b31b99a17e59fae0df54cb6b9599268d8ea3ba2677a94bdce148b597f0c229055c3d881fffaa92b843dad324c8160194f90ee')

build() {
  cd $pkgname-c-$pkgver
  cmake -S . -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev \
    -DBUILD_SHARED_LIBS=ON \
    -DMSGPACK_ENABLE_STATIC=OFF \
    -DMSGPACK_BUILD_EXAMPLES=OFF \
    -DMSGPACK_BUILD_TESTS=ON
  cmake --build build
}

check() {
  cd $pkgname-c-$pkgver
  cmake --build build --target test
}

package() {
  cd $pkgname-c-$pkgver
  DESTDIR="$pkgdir" cmake --install build
}
