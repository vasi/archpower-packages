# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase=lpeg
pkgname=(lua-$pkgbase lua51-$pkgbase lua52-$pkgbase lua53-$pkgbase)
pkgver=1.1.0
pkgrel=4
_rockrel=2
pkgdesc='Parsing Expression Grammars For Lua'
arch=(x86_64 'powerpc')
url="https://www.inf.puc-rio.br/~roberto/$pkgbase/"
license=(MIT)
depends=(glibc)
makedepends=(lua lua51 lua52 lua53 luarocks)
_archive="$pkgbase-$pkgver"
source=("https://luarocks.org/manifests/gvvaughan/$_archive-$_rockrel.src.rock"
        LICENSE-upstream)
sha256sums=('836d315b920a5cdd62e21786c6c9fad547c4faa131d5583ebca64f0b6595ee76'
            '6be7688c9c648e7c7cde693081be938c869a10aa4de09fe0a4c4c1d66b3cf03d')

_package() {
	pkgdesc+=" for Lua $1"
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		install --deps-mode none --no-manifest -- "${source[0]##*/}"
	install -Dm0664 LICENSE-upstream "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua-lpeg() {
	_package 5.4
}

package_lua51-lpeg() {
	_package 5.1
}

package_lua52-lpeg() {
	_package 5.2
}

package_lua53-lpeg() {
	_package 5.3
}
