# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=3.1.6
pkgrel=8
pkgdesc='A tool to generate compilation database for clang tooling'
arch=('x86_64' 'powerpc')
url='https://github.com/rizsotto/Bear'
license=('GPL-3.0-or-later')
depends=(
  'glibc'
  'gcc-libs'
  'grpc'
  'libgrpc++.so'
  'fmt'
  'spdlog'
  'abseil-cpp'
  'protobuf'
  'openssl'
  'c-ares'
  're2'
)
makedepends=(
  'git'
  'cmake'
  'nlohmann-json'
  'gtest'
  'python'
  'llvm'
)
source=(
  "$pkgname::git+$url.git#tag=$pkgver"
  "disable-lit-tests.patch"
)
sha512sums=('583e9a0f9edc609a17f9697379787b1c359e386d22c25ef0d8cdd756c474672b6476747baef656618ffde453820d4f429203001cacfe220b160192c90e31b6a7'
            '9cb74c653d514fb0802da1b2cf103fdcf25a8aa314f4e7023ce851044a6232b87c340814d61a24aa5af3f4aecdf7c0560262be2b68f19f3566164f2babb12f87')
b2sums=('91b75a510a4d2818411854e732c4dd83027a9c34f9a630d7a31901c3c66a4ba88cdbb685c1df36a61b392107f209adcf4a32164a1efdc4c12e2a08b1a117115f'
        '9bc603ac82c4b2c8bf1263e7d24205f7445b265da16c21935f58c651c5ed78fdb81d00565a3f2b81415831564b81384cd71c799f58c85bce0445ecdf67b7c3b8')

# XXX if this is moved to build, tests fail.
# there must be some environment variables that
# are discarded inbetween functions.
prepare() {
  # temporary workaround for failing bear::func tests
  # lit errors out when failing to acquire semaphore locks in python multiprocessing module
  # probably related to chroot environment but unrelated to bear::func tests which are not even executed
  patch -d "$pkgname" -p1 -i ../disable-lit-tests.patch

  cmake \
    -B build \
    -S "$pkgname" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_SYSCONFDIR=/etc \
    -D CMAKE_INSTALL_LIBEXECDIR="lib/$pkgname"
}

build() {
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
