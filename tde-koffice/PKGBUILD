# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: e1z0 <justinas at eofnet dot lt>

_mod=koffice
_cat=applications/office/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.4
pkgrel=1
pkgdesc="TDE KOffice suite"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-extra')
depends=('tde-tdebase')
makedepends=('pkgconfig' 'automake')
provides=("${_mod}")
conflicts=("trinity-${_kdemod}" "kdemod3-${_kdemod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz"
        "xsltfilter.patch")
md5sums=('b768c65065a2c58022635402901bf709'
         '128425ce1dc1536d822bb7ec0af91b92')
install=''

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity
[ -n "$TQTDIR" ] || TQTDIR=${TDEDIR}/tqt3

prepare() {
  cd ${srcdir}/${pkgname#*-}-trinity-${pkgver}
  patch -Np1 -i ../xsltfilter.patch
}

build() {
  ## Generate config files and update with autoreconf
  cd ${srcdir}/${pkgname#*-}-trinity-${pkgver}

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh
  cp -a /usr/share/libtool/build-aux/config.guess ./admin/
  cp -a /usr/share/libtool/build-aux/config.sub ./admin/

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  export PKG_CONFIG_PATH=${TDEDIR}/lib/pkgconfig:${TQTDIR}/lib/pkgconfig:${PKG_CONFIG_PATH}

  ## configure
  msg "Configuring - ${pkgname}..."
  CFLAGS="${CFLAGS} -fpermissive" \
  CXXFLAGS="${CXXFLAGS} -fpermissive" \
  ./configure \
    --prefix=${TDEDIR} \
    --with-tqt-dir=${TQTDIR} \
    --with-tqt-includes=${TQTDIR}/include \
    --with-tqt-libraries=${TQTDIR}/lib \
    --sysconfdir=${TDEDIR}/etc \
    --localstatedir=/var \
    --enable-closure

  msg "Building - ${pkgname#*-}..."
  make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/${pkgname#*-}-trinity-${pkgver}            # use for libtool
  make -j1 DESTDIR="${pkgdir}" install
}
