# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-polkit
pkgver=1.28.1
pkgrel=2
pkgdesc="PolicyKit integration for the MATE desktop"
url="https://mate-desktop.org"
arch=('x86_64' 'powerpc')
license=('GPL-2.0-or-later')
groups=('mate')
depends=('gtk3' 'polkit' 'gettext')
makedepends=('accountsservice' 'autoconf-archive' 'mate-common' 'gobject-introspection')
conflicts=('mate-polkit-gtk3')
replaces=('mate-polkit-gtk3')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/mate-polkit/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('b7dd0999f1570e41939997f075ddbf459650c6572dd89a63b3a1963d60427001')

prepare() {
    cd "${pkgname}-${pkgver}"
    ./autogen.sh
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
                --prefix=/usr \
                --libexecdir="/usr/lib/${pkgname}" \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --enable-introspection
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
