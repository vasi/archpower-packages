# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-control-center
pkgver=1.28.1
pkgrel=2
pkgdesc="The Control Center for MATE"
url="https://mate-desktop.org"
arch=('x86_64' 'powerpc')
license=('GPL-2.0-or-later')
depends=('mate-menus' 'mate-settings-daemon' 'marco' 'libxss' 'mate-desktop'
         'gettext' 'accountsservice' 'mate-panel' 'libcanberra' 'udisks2' 'libayatana-appindicator')
makedepends=('autoconf-archive' 'glib2-devel' 'mate-common' 'itstool' 'yelp-tools')
groups=('mate')
conflicts=('mate-control-center-gtk3')
replaces=('mate-control-center-gtk3')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mate-desktop/mate-control-center/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('0890c1bafc10a77ae7aea4edc37951fa69cbce2552ce199a7e49cb55de8af71b')

prepare() {
    cd "${pkgname}-${pkgver}"
    ./autogen.sh
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --disable-update-mimedb
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm "${pkgdir}/usr/share/applications/mimeinfo.cache"
}

# vim:set sw=4 et:
