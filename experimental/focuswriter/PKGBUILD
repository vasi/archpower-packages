# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=focuswriter
pkgver=1.8.13
pkgrel=1
pkgdesc='Simple, distraction-free word processor'
arch=('x86_64' 'powerpc')
url='https://gottcode.org/focuswriter/'
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'qt6-multimedia')
makedepends=('cmake' 'qt6-tools')
source=("https://gottcode.org/$pkgname/$pkgname-$pkgver.tar.bz2"{,.asc})
sha256sums=('736662c0ec6ca9d6ae93b18f637963e9f5a914b25027d07b34723ca91b4aa98b'
            'SKIP')
validpgpkeys=('57E4D43C10CD7EDB8360C84774FFC7F02C4F6B37')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
