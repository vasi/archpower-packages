# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Ionut Biru  <ibiru@archlinux.ro>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=2.1
pkgrel=2
pkgdesc='Fast and lightweight IDE'
arch=(x86_64 'powerpc')
url='https://www.geany.org/'
license=(GPL)
depends=(gtk3)
makedepends=(doxygen intltool python-lxml)
optdepends=('geany-plugins: additional functionality'
            'vte3: embedded terminal support')
source=("https://download.geany.org/$pkgname-$pkgver.tar.bz2")
b2sums=('b6249ea8c8637516c4133e6cfb6cd8cde2495f7b507342918f7850feb4f447ddf6513a9f12fc67cd0c909543346eeb4cff0ddfd26658dee9128fa899770a1bb7')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-gtk3 --enable-gtkdoc-header --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
