# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi7
pkgver=3.3
pkgrel=4
pkgdesc='Portable foreign function interface library'
arch=(x86_64 powerpc64le powerpc)
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
options=(!distcc)
source=(https://sourceware.org/pub/libffi/libffi-$pkgver.tar.gz
	powerpc-fix.patch
        powerpc-fix-2.patch
        powerpc64-fix.patch
        powerpc64-fix-2.patch
        libffi-race-condition.patch)
sha256sums=('72fba7922703ddfa7a028d513ac15a85c8d54c8d67f55fa5a4802885dc652056'
            'f80413d386314a29e556afe6e8f5786ccd92752a9e840e447023e4333dfb8782'
            '08f62dd6b0407baaf4b4c88b4adba89c22aac4f0df1d74d7b5463cfeff4a0ce9'
            'd99004460fa7dce69d233fdb77697ff86e227e38d1564778d649ba88e3db09e7'
            '0c595be6cf09213261aad67843e93e269dd9db751ada5428776fb73e6be240fc'
            '2195ae52e7fe1890642d8fd6504840aa74961f335351471c0228292ebc7d1a93')

prepare() {
  cd libffi-$pkgver

  # thank you Void
  patch -Np0 -i ${srcdir}/powerpc-fix.patch
  patch -Np0 -i ${srcdir}/powerpc-fix-2.patch
  patch -Np0 -i ${srcdir}/powerpc64-fix.patch
  patch -Np0 -i ${srcdir}/powerpc64-fix-2.patch
  patch -Np0 -i ${srcdir}/libffi-race-condition.patch
}

build() {
  cd libffi-$pkgver
  ./configure --prefix=/usr --disable-static \
    --enable-pax_emutramp
  make
}

check() {
  make -C libffi-$pkgver check
}

package() {
  cd libffi-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf $pkgdir/usr/{include,lib/libffi.so,lib/pkgconfig,share/info,share/man}
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
